#!/home/teppey/opt/bin/gosh
;; vim:set ft=scheme fileencoding=utf-8:

;;;
;;; gosh-rlx - Gauche-readline repl with listener
;;;
;;;
;;; Readline module for Gauche
;;; <http://www.netlab.is.tsukuba.ac.jp/~yokota/izumi/gauche/>
;;;
;;;
;;; Please send bug reports and comments to:
;;;   temada@gmail.com   Teppei Hamada
;;;
;;;
;;; [user] -> [readline repl] ------> +------------+
;;;                                   | named pipe | -> [evaluator]
;;; [other process (e.g. vim)] -----> +------------+
;;;


;; TODO
;;   - show stacktrace on error
;;   - allow multiple invocation?
;;   - handling command line options like gosh(-I,-A,...)
;;   - illustrated comment
;;   - testing infinite loop
;;   - signal handling

(use gauche.threads)
(use gauche.interactive)
(use gauche.listener :only (complete-sexp?))
(use srfi-1 :only (append-map))
(use file.util)
(use term.readline)

(define (start-evaluator! in out)
  (define (print-error condition)
    (if (and (condition? condition)
             (condition-has-type? condition <message-condition>))
      (format #t "Error: ~a\n" (condition-ref condition 'message))
      (format #t "Error: ~a\n" (x->string condition))))
  (define (interactive?)
    (eq? (thread-specific (current-thread)) 'root))
  (define (eval-print sexps)
    (dolist (sexp sexps)
      (call-with-values
        (lambda () (eval sexp (interaction-environment)))
        (lambda results
          (dolist (result results)
            (write/ss result)
            (newline))))))
  (define (repl in out)
    ;; Binding order is important to avoid deadlock.
    (let* ([code (call-with-input-file in port->string)]
           [output (if (interactive?)
                     (open-output-file out)
                     (current-output-port))])
      (let/cc break
        (with-output-to-port output
          (lambda ()
            (unless (interactive?) (newline))
            (guard (exc (else (print-error exc)
                              (break #f)))
              (call-with-input-string code
                (.$ eval-print port->sexp-list))))))
      (if (interactive?)
        (begin (thread-specific-set! (current-thread) #f)
               (close-output-port output))
        (display "gosh> "))
      (flush)
      (repl in out)))

  (thread-start! (make-thread (cut repl in out))))

(define (history-file)
  (build-path (home-directory) ".gosh_history"))

(define (rl-init!)
  (define (all-functions-list)
    (append-map (^m (hash-table-map (module-table m)
                    (^(sym gloc) (symbol->string sym))))
                (all-modules)))
  (rl-read-history! (history-file))
  (rl-set-application-name!            "Gauche")
  (rl-set-basic-word-break-characters! " \t\n()',\";")
  (rl-set-basic-quote-characters!      "\"")
  (rl-set-prompt!                      :ps1 "gosh> " :ps2 "..... ")
  (rl-set-completion-list-function!    (lambda () (all-functions-list)))
  (rl-stifle-history!                  1000))

(define (start-interaction! in out eval-thread)
  (let1 readline-port (make-readline-input-port)
    (rl-init!)
    (let/cc break
      (while #t
        (rl-set-prompt-state! 'ps1)
        (let loop ([line (read-line readline-port)])
          (cond [(eof-object? line)
                 (break #f)]
                [(complete-sexp? line)
                 (thread-specific-set! eval-thread 'root)
                 ;; Request evaluation
                 (call-with-output-file in (cut display line <>))
                 ;; Receive results
                 (call-with-input-file out
                   (^(in) (until (read-line in) eof-object? => line
                            (print line))))
                 (rl-commit-history!)]
                [else
                  ;; Continuous line; add newline that because read-line
                  ;; remove line-terminator.
                  (loop (string-append
                          #`",|line|\n" (read-line readline-port)))]))))))

(define (channel-initialize!)
  (let* ([dir (build-path (home-directory) #`".gosh-rlx")]
         [in (build-path dir "in")]
         [out (build-path dir "out")])
    (unless (file-exists? dir) (sys-mkdir dir #o0700))
    (unless (file-exists? in) (sys-mkfifo in #o0600))
    (unless (file-exists? out) (sys-mkfifo out #o0600))
    (values dir in out)))

(define (main _)
  (receive (dir in out)
    (channel-initialize!)
    (dynamic-wind
      (lambda () #f)
      (lambda () (start-interaction! in out (start-evaluator! in out)))
      (lambda ()
        (remove-files `(,in ,out))
        (rl-write-history (history-file))))
    (exit 0)))
